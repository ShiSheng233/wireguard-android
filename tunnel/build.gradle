plugins {
    alias(libs.plugins.android.library)
}

version wireguardVersionName
group = GROUP

android {
    compileSdk 33
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.wireguard.android.tunnel'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode = wireguardVersionCode.toInteger()
        versionName = wireguardVersionName
    }
    externalNativeBuild {
        cmake {
            path 'tools/CMakeLists.txt'
        }
    }
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    buildTypes {
        release {
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${GROUP}", "-DGRADLE_USER_HOME=${project.gradle.gradleUserHomeDir}"
                }
            }
        }
        debug {
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${GROUP}.debug", "-DGRADLE_USER_HOME=${project.gradle.gradleUserHomeDir}"
                }
            }
        }
    }
    lint {
        disable 'LongLogTag', 'NewApi'
    }
}

dependencies {
    implementation libs.androidx.annotation
    implementation libs.androidx.collection
    compileOnly libs.jsr305
    testImplementation libs.junit
}

apply from: "publish.gradle"
